(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "28-Apr-2023 07:55:08" {DSK}<home>medley>il>STRINGSCOPE.;52 18973  

      :EDIT-BY "PA"

      :CHANGES-TO (VARS STRINGSCOPECOMS)
                  (FNS SET.SSCWIN.TITLE)

      :PREVIOUS-DATE "28-Apr-2023 06:08:59" {DSK}<home>medley>il>STRINGSCOPE.;51)


(PRETTYCOMPRINT STRINGSCOPECOMS)

(RPAQQ STRINGSCOPECOMS
       ((FNS APPEND.CHAR ATLEASTNCHRSP CREATE.SSWINDOW EXTRACT.STRINGS HANDLE.SSMENU LIST.STRINGS 
             PRINTABLEP REPAINT.SSWINDOW RESHAPE.SSWINDOW SET.SSCWIN.TITLE SSCMD.EXIT SSCMD.MINLEN 
             SSCMD.RESET SSCMD.SORT STRINGS STRINGSCOPE)
        (VARS)
        (INITVARS (SSCOPE.MIN.LEN 4))
        (COMMANDS STRINGS)
        (FUNCTIONS WITH.INPUT.FILE)))
(DEFINEQ

(APPEND.CHAR
  [LAMBDA (STRING CODE)                                      (* ; "Edited 25-Jan-2023 09:48 by PA")

         (* Return a copy of STRING with the character of code CODE appended to the end.)

    (CONCAT STRING (CHARACTER CODE])

(ATLEASTNCHRSP
  [LAMBDA (STRING MIN.LEN)                                   (* ; "Edited 29-Jan-2023 11:25 by PA")
                                                             (* ; "Edited 25-Jan-2023 09:37 by PA")
                                                             (* Return STRING if it has a length of 
                                                             at least MIN.LEN characters, NIL 
                                                             otherwise.)
    (AND (STRINGP STRING)
         (SMALLP MIN.LEN)
         (IGEQ (NCHARS STRING)
               MIN.LEN])

(CREATE.SSWINDOW
  [LAMBDA (FILE STRINGS MIN.LENGTH)                          (* ; "Edited 28-Apr-2023 06:02 by PA")
                                                             (* ; "Edited 26-Apr-2023 04:42 by PA")
                                                             (* ; "Edited 22-Apr-2023 11:38 by PA")
                                                             (* ; "Edited 15-Apr-2023 12:56 by PA")
                                                             (* ; "Edited  1-Apr-2023 12:04 by PA")
                                                             (* ; "Edited 27-Jan-2023 10:55 by PA")
                                                             (* ; "Edited 26-Jan-2023 10:48 by PA")

         (* Create and return a window containing the output of Stringscope.
         Based on an example in IRM.)

    (PROG (WINDOW)
          (SETQ WINDOW (CREATEMENUEDWINDOW (create MENU
                                                  ITEMS _ '((Get GET "Open new file")
                                                            (Find FIND "Search for specific string")
                                                            (Sort SORT "Sort strings"
                                                                  (SUBITEMS (Ascending ASCENDING 
                                                                    "Sort strings in ascending order"
                                                                                   )
                                                                         (Descending DESCENDING 
                                                                   "Sort strings in descending order"
                                                                                )))
                                                            ("Min Len" MINLEN 
                                                                   "Set minimum string length")
                                                            (Reset RESET 
                                                        "Redisplay the strings as read from the file"
                                                                   )
                                                            (Exit EXIT "Quit the program"))
                                                  CENTERFLG _ T
                                                  MENUFONT _ '(MODERN 12)
                                                  TITLE _ "Commands"
                                                  WHENSELECTEDFN _ (FUNCTION HANDLE.SSMENU))
                              (SET.SSCWIN.TITLE NIL FILE MIN.LENGTH)
                              'RIGHT))
          (WINDOWPROP WINDOW 'PROMPTAREA (GETPROMPTWINDOW WINDOW))
                                                             (* Name of the current file from which 
                                                             the strings are read.)
          (WINDOWPROP WINDOW 'FILE FILE)                     (* List of strings read from the 
                                                             current file.)
          (WINDOWPROP WINDOW 'STRINGS STRINGS)               (* List of processed strings output by 
                                                             the latest command.)
          (WINDOWPROP WINDOW 'STRINGS.CACHE STRINGS)
          (WINDOWPROP WINDOW 'MIN.LENGTH MIN.LENGTH)
          (WINDOWPROP WINDOW 'REPAINTFN (FUNCTION REPAINT.SSWINDOW))
          (WINDOWPROP WINDOW 'RESHAPEFN (FUNCTION RESHAPE.SSWINDOW))
          (WINDOWPROP WINDOW 'SCROLLFN (FUNCTION SCROLLBYREPAINTFN))
          (RESHAPE.SSWINDOW WINDOW)
          (RETURN WINDOW])

(EXTRACT.STRINGS
  [LAMBDA (STREAM MIN.CHARS)                                 (* ; "Edited 29-Jan-2023 10:25 by PA")
                                                             (* ; "Edited 27-Jan-2023 10:12 by PA")
                                                             (* ; "Edited 25-Jan-2023 09:43 by PA")
                                                             (* ; "Edited 22-Jan-2023 10:19 by PA")
                                                             (* ; "Edited 19-Jan-2023 10:28 by PA")

         (* Return a list of the printable strings in STREAM in the same order in which 
         they appear. The strings must be at least SSCOPE.MIN.LEN characters in length.)

    (PROG (MIN.LENGTH (STATE 'BINARY)
                 (CODE 0)
                 (BUFFER "")
                 (STRINGS NIL))                              (* State machine that matches 
                                                             printable strings.)
          (SETQ MIN.LENGTH (OR MIN.CHARS SSCOPE.MIN.LEN))
          (do (SETQ CODE (BIN STREAM))
              (SELECTQ STATE
                  (BINARY (if (PRINTABLEP CODE)
                              then (SETQ BUFFER (APPEND.CHAR BUFFER CODE))
                                   (SETQ STATE 'MAYBE.STRING)))
                  (MAYBE.STRING (if (PRINTABLEP CODE)
                                    then (SETQ BUFFER (APPEND.CHAR BUFFER CODE))
                                         (if (ATLEASTNCHRSP BUFFER MIN.LENGTH)
                                             then (SETQ STATE 'STRING))
                                  else (SETQ BUFFER "")
                                       (SETQ STATE 'BINARY)))
                  (STRING (if (PRINTABLEP CODE)
                              then (SETQ BUFFER (APPEND.CHAR BUFFER CODE))
                            else (SETQ STRINGS (CONS BUFFER STRINGS))
                                 (SETQ BUFFER "")
                                 (SETQ STATE 'BINARY)))
                  (ERROR STATE "unknown State Machine state")) while (NOT (EOFP STREAM)))

         (* The state machine doesn't handle the case of a printable string that ends at 
         the end of the file, so check if this is the case.)

          (if (ATLEASTNCHRSP BUFFER MIN.LENGTH)
              then (SETQ STRINGS (CONS BUFFER STRINGS)))
          (RETURN (DREVERSE STRINGS])

(HANDLE.SSMENU
  [LAMBDA (ITEM MENU KEY)                                    (* ; "Edited 27-Apr-2023 10:57 by PA")
                                                             (* ; "Edited 22-Apr-2023 11:41 by PA")
                                                             (* ; "Edited 21-Apr-2023 11:12 by PA")
                                                             (* ; "Edited 20-Apr-2023 10:11 by PA")
                                                             (* ; "Edited  1-Apr-2023 12:04 by PA")

         (* Main menu handler that dispatches based on the selected option and executes 
         the appropriate command.)

    (LET* ((MENU.WINDOW (WFROMMENU MENU))
           (MAIN.WINDOW (MAINWINDOW WINDOW T)))
          (if (OR (EQ KEY 'MIDDLE)
                  (EQ KEY 'RIGHT))
              then (RETURN NIL))
          (SELECTQ (CADR ITEM)
              (GET (PROMPTPRINT ITEM))
              (FIND (PROMPTPRINT ITEM))
              (SORT (SSCMD.SORT MAIN.WINDOW))
              (ASCENDING (SSCMD.SORT MAIN.WINDOW))
              (DESCENDING [SSCMD.SORT MAIN.WINDOW (FUNCTION (LAMBDA (S1 S2)
                                                              (ALPHORDER S2 S1])
              (MINLEN (SSCMD.MINLEN MAIN.WINDOW))
              (RESET (SSCMD.RESET MAIN.WINDOW))
              (EXIT (SSCMD.EXIT MAIN.WINDOW))
              (T (PROMPTPRINT "Unknown menu option"])

(LIST.STRINGS
  [LAMBDA (STRINGS STREAM)                                   (* ; "Edited 22-Jan-2023 10:29 by PA")

         (* Print to STREAM a report with a list of the strings in the STRINGS list.)

    (for STR in STRINGS do (PRIN1 STR STREAM)
                           (TERPRI STREAM])

(PRINTABLEP
  [LAMBDA (CHARCODE)                                         (* ; "Edited 21-Jan-2023 07:12 by PA")
                                                             (* ; "Edited 19-Jan-2023 10:00 by PA")
                                                             (* Return T if CHARCODE is a printable 
                                                             ASCII code between SPACE and ~.)
                                                             (* ; "Edited 18-Jan-2023 11:52 by PA")
    (AND (SMALLP CHARCODE)
         (IGEQ CHARCODE 32)
         (ILEQ CHARCODE 127])

(REPAINT.SSWINDOW
  [LAMBDA (WINDOW REGION)                                    (* ; "Edited 20-Apr-2023 10:21 by PA")
                                                             (* ; "Edited 29-Jan-2023 11:27 by PA")
                                                             (* ; "Edited 26-Jan-2023 10:26 by PA")
                                                             (* Repaint the Stringscope window.
                                                             Based on an example in IRM.)
    (MOVETO (WINDOWPROP WINDOW 'SSORIGX)
           (WINDOWPROP WINDOW 'SSORIGY)
           WINDOW)
    (LIST.STRINGS (WINDOWPROP WINDOW 'STRINGS.CACHE)
           WINDOW])

(RESHAPE.SSWINDOW
  [LAMBDA (WINDOW)                                           (* ; "Edited 29-Jan-2023 11:28 by PA")
                                                             (* ; "Edited 26-Jan-2023 10:44 by PA")
                                                             (* Resize the Stringscope window.
                                                             Based on an example in IRM.)
    (PROG (BTM)
          (DSPRESET WINDOW)
          (WINDOWPROP WINDOW 'SSORIGX (DSPXPOSITION NIL WINDOW))
          (WINDOWPROP WINDOW 'SSORIGY (DSPYPOSITION NIL WINDOW))
          (REPAINT.SSWINDOW WINDOW)
          (WINDOWPROP WINDOW 'EXTENT (create REGION
                                            LEFT _ 0
                                            BOTTOM _ [SETQ BTM (IPLUS (DSPYPOSITION NIL WINDOW)
                                                                      (FONTPROP WINDOW 'ASCENT]
                                            WIDTH _ (WINDOWPROP WINDOW 'WIDTH)
                                            HEIGHT _ (IDIFFERENCE (WINDOWPROP WINDOW 'HEIGHT)
                                                            BTM])

(SET.SSCWIN.TITLE
  [LAMBDA (WINDOW FILENAME MINLEN)                           (* ; "Edited 28-Apr-2023 07:49 by PA")

         (* If WINDOW is not NIL set the title of Stringscope's main window based on the 
         name FILENAME of the current file and the minimum length of strings MINLEN.
         In either case return the title.)
                                                             (* ; "Edited 28-Apr-2023 07:47 by PA")
    (LET ((TITLE (CONCAT "Stringscope " (MKATOM FILENAME)
                        " Min Len: " MINLEN)))
         (AND WINDOW (WINDOWPROP WINDOW 'TITLE TITLE))
         TITLE])

(SSCMD.EXIT
  [LAMBDA (WINDOW)                                           (* ; "Edited 22-Apr-2023 11:40 by PA")
                                                             (* Quits the program.)
    (CLOSEW WINDOW])

(SSCMD.MINLEN
  [LAMBDA (WINDOW)                                           (* ; "Edited 28-Apr-2023 06:06 by PA")

         (* Prompt the user for a minimum length, extract the strings with the new length 
         from the current file, and refresh the view.)

    (LET* [(STREAM (WINDOWPROP WINDOW 'PROMPTAREA))
           (CURLEN (WINDOWPROP WINDOW 'MIN.LENGTH))
           (NEWLEN (PROGN (TERPRI STREAM)
                          (MKATOM (PROMPTFORWORD "Min Len:" 4 NIL STREAM]
          (if (AND (SMALLP NEWLEN)
                   (GREATERP NEWLEN 0))
              then (AND (NOT (EQ NEWLEN CURLEN))
                        (WINDOWPROP WINDOW 'MIN.LENGTH NEWLEN)
                        (LET [(NEWSTRINGS (WITH.INPUT.FILE (STR (WINDOWPROP WINDOW 'FILE))
                                                 (EXTRACT.STRINGS STR NEWLEN]
                             (WINDOWPROP WINDOW 'STRINGS NEWSTRINGS)
                             (WINDOWPROP WINDOW 'STRINGS.CACHE NEWSTRINGS)
                             (SET.SSCWIN.TITLE WINDOW (WINDOWPROP WINDOW 'FILE)
                                    NEWLEN)
                             (RESHAPE.SSWINDOW WINDOW)))
            else (PRINTOUT STREAM T "Invalid length" %, NEWLEN])

(SSCMD.RESET
  [LAMBDA (WINDOW)                                           (* ; "Edited 22-Apr-2023 11:35 by PA")

         (* Displays the strings last read from the file in the same order they are in the 
         file.)

    (WINDOWPROP WINDOW 'STRINGS.CACHE (WINDOWPROP WINDOW 'STRINGS))
    (RESHAPE.SSWINDOW WINDOW])

(SSCMD.SORT
  [LAMBDA (WINDOW COMPAREFN)                                 (* ; "Edited 21-Apr-2023 11:23 by PA")
                                                             (* ; "Edited 20-Apr-2023 10:27 by PA")
                                                             (* ; "Edited 20-Apr-2023 10:08 by PA")

         (* Sort the list of strings stored in WINDOW using the COMPAREFN comparison 
         function and update the view.)

    [WINDOWPROP WINDOW 'STRINGS.CACHE (SORT (COPY (WINDOWPROP WINDOW 'STRINGS))
                                            (OR COMPAREFN (FUNCTION ALPHORDER]
    (RESHAPE.SSWINDOW WINDOW])

(STRINGS
  [LAMBDA (FILE MIN.LENGTH NEWWIN)                           (* ; "Edited 18-Feb-2023 10:21 by PA")
                                                             (* ; "Edited 17-Feb-2023 09:05 by PA")
                                                             (* ; "Edited 12-Feb-2023 10:48 by PA")

         (* Lists the printable strings in binary file FILE in a new window if NEWWIN is 
         T, otherwise prints them to the primary output #.(SEDIT::MAKE-BROKEN-ATOM ".") 
         The strings must be at least MIN.LENGTH characters in length.
         Depending on NEWWIN returns the primary output stream or the new window if the 
         file is processed with no issues, NIL otherwise.)

    (LET [(STRINGS (WITH.INPUT.FILE (STREAM FILE)
                          (EXTRACT.STRINGS STREAM MIN.LENGTH]
         (if STRINGS
             then (if NEWWIN
                      then (CREATE.SSWINDOW FILE STRINGS (OR MIN.LENGTH SSCOPE.MIN.LEN))
                    else (LIST.STRINGS STRINGS)
                         (OUTPUT))
           else (PROMPTPRINT (CONCAT "No strings in " FILE " or can't open file."))
                NIL])

(STRINGSCOPE
  [LAMBDA (FILE MIN.LENGTH)                                  (* ; "Edited 18-Feb-2023 10:17 by PA")
                                                             (* ; "Edited 17-Feb-2023 10:04 by PA")
                                                             (* ; "Edited 13-Feb-2023 06:15 by PA")
                                                             (* ; "Edited 29-Jan-2023 10:25 by PA")
                                                             (* ; "Edited 27-Jan-2023 10:52 by PA")
                                                             (* ; "Edited 24-Jan-2023 06:27 by PA")
                                                             (* ; "Edited 22-Jan-2023 10:46 by PA")
                                                             (* ; "Edited 19-Jan-2023 10:35 by PA")

         (* Lists in a new window the printable strings in binary file FILE.
         The strings must be at least MIN.LENGTH characters in length.
         Returns the new window if the file is processed with no issues, NIL otherwise.)

    (STRINGS FILE MIN.LENGTH T])
)

(RPAQ? SSCOPE.MIN.LEN 4)

(DEFCOMMAND (STRINGS :EVAL) (FILE &OPTIONAL (MIN.LENGTH SSCOPE.MIN.LEN)) (STRINGS FILE MIN.LENGTH)
   (CL:VALUES))

(DEFMACRO WITH.INPUT.FILE ((STREAM FILE)
                           &BODY BODY &AUX (RESULT (GENSYM))
                           (VALUE (GENSYM)))                 (* ; "Edited  3-Apr-2023 05:13 by PA")
                                                             (* ; "Edited 16-Feb-2023 10:50 by PA")

   (* ;; "Opens an input stream to FILE and evaluates the forms in BODY with the stream bound to STREAM.  Returns the value of the last form in BODY, or NIL if FILE can not be opened.")
                                                             (* ; "Edited 16-Feb-2023 10:38 by PA")
   `(PROG NIL
          [SETQ ,RESULT (NLSETQ (OPENSTREAM ,FILE 'INPUT]
          (if ,RESULT
              then (SETQ ,STREAM (CAR ,RESULT))
                   (SETQ ,VALUE (PROGN ,@BODY))
                   (CLOSEF ,STREAM)
                   (RETURN ,VALUE)
            else (RETURN NIL))))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (784 17868 (APPEND.CHAR 794 . 1051) (ATLEASTNCHRSP 1053 . 1676) (CREATE.SSWINDOW 1678 . 
5429) (EXTRACT.STRINGS 5431 . 7951) (HANDLE.SSMENU 7953 . 9432) (LIST.STRINGS 9434 . 9753) (PRINTABLEP
 9755 . 10378) (REPAINT.SSWINDOW 10380 . 11096) (RESHAPE.SSWINDOW 11098 . 12282) (SET.SSCWIN.TITLE 
12284 . 12929) (SSCMD.EXIT 12931 . 13163) (SSCMD.MINLEN 13165 . 14436) (SSCMD.RESET 14438 . 14783) (
SSCMD.SORT 14785 . 15454) (STRINGS 15456 . 16696) (STRINGSCOPE 16698 . 17866)) (18024 18950 (
WITH.INPUT.FILE 18024 . 18950)))))
STOP
